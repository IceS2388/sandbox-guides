= Recommendations
:user_name: 'Misty Williams'
:movie_name: 'Matrix'
:experimental:
:icon: font


== Personalized Product Recommendations With Neo4j

++++
<div class="col-lg-8 ng-scope">
++++

++++
<div class="col-lg-6 ng-scope">
++++

=== Recommendations

Personalized product recommendations can increase conversions, sales rates and provide a better experice for users. In this Neo4j Browser guide, we'll take a look at how you can generate graph-based real time personalized product recommendations using a dataset of movies and movie ratings, but these techniques can be applied to many different types of products or content.

+++
</div>
+++

++++
<div class="col-lg-6 ng-scope">
++++
=== Graph Based Recommendations

Generating personalized recommendations is one of the most common use cases for a graph database. Some of the main benefits of using graphs to generate recommendations include:

1. *Performance*. Index-free adjacency allows for calculating recommendations in real time, ensuring the recommendation is always relevant and reflecting up to date information.
1. *Data model*. The labeled propety graph model allows for easily combining data sets from multiple sources, allowing enterprises to **unlock value from previously separated data silos.**
+++
</div>
+++

[subs=attributes]
++++
<img src="{img}/title1.png" class="img-responsive">
++++

++++
</div>
++++

++++
<div class="col-lg-4 ng-scope">
++++
=== The Data


[subs=attributes]
++++
<img src="{img}/openmoviegraph.png" class="img-responsive">
++++

.Data Sources
****
* http://www.omdbapi.com/[Open Movie Database]
* https://grouplens.org/datasets/movielens/[MovieLens]
****


*Let's get started! Click the arrow above to move to the next pane in this guide.*

++++
</div>
++++

//== Recommendations with neo4j
//
//++++
//<div class="col-lg-6 ng-scope">
//++++
//=== Silo 1
//
//[subs=attributes]
//++++
//<img src="{img}/silo1.png" class="img-responsive" height="90%">
//++++
//
//++++
//</div>
//++++
//
//++++
//<div class="col-lg-6 ng-scope">
//++++
//
//=== Silo 2
//
//[subs=attributes]
//.Caption here?
//++++
//<img src="{img}/silo2.png" class="img-responsive">
//++++
//
//++++
//</div>
//++++
//
//== The Labeled Property Graph Datamodel
//
//++++
//<div class="col-lg-3 ng-scope">
//++++
//=== Elements
//
//* Nodes
//- Entities
//- Labels
//- Properties
//
//++++
//</div>
//++++
//
//++++
//<div class="col-lg-9 ng-scope">
//++++
//
//.Caption for the image, explain the data model
//[subs=attributes]
//++++
//<img src="{img}/lpg.png" class="img-responsive">
//++++
//
//++++
//</div>
//++++

== The Open Movie Graph Data Model
++++
<div class="col-lg-4 ng-scope">
++++


=== The Labled Property Graph
The data model of graph databases is called the Labeled Property Graph model.

*Nodes* The entities in the data.

*Labels* Each node can have one or more *label*  that specifies the type of the node.

*Relationships* - connect two nodes. Has a single direction and type.

*Properties* - key-value pair properties can be stored on both nodes and relationships.

=== Data Silos
We said earlier that one of the benefits of using graphs for recommendations is the ability to combine data from multiple silos. That's exactly what we've done here:

*Product Catalog* Data describing movies comes from the product catalog silo.

*User Purchases / Reviews* Data on user purchases and reviews comes from the user or transaction silo.

By combining these two in the graph we are able to query across datasets to generate personalized product recommendations.

++++
</div>
++++

++++
<div class="col-lg-8 ng-scope">
++++

[subs=attributes]
++++
<img src="{img}/datamodel.png" class="img-responsive">
++++

++++
<div class="col-lg-4 ng-scope">
++++

=== Nodes

`Movie`, `Actor`, `Director`, `User`, `Genre` are the labels used in this example.

++++
</div>
++++

++++
<div class="col-lg-4 ng-scope">
++++

=== Relationships

`ACTED_IN`, `IN_GENRE`, `DIRECTED`, `RATED` are the relationships used in this example.

++++
</div>
++++

++++
<div class="col-lg-4 ng-scope">
++++

=== Properties

`title`, `name`, `year`, `rating` are some of the properties used in this example.

++++
</div>
++++


++++
</div>
++++

== Intro To Cypher

In order to work with our labeled property graph we need a query language for graphs. 


++++
<div class="col-lg-3 ng-scope">
++++
=== Graph Patterns

Cypher is the query language for graphs and is centered around *graph patterns*. Graph patterns are expressed in Cypher using ASCII-art like syntax.

*Nodes*

*Relationships*

*Aliases*


=== Predicates

Filters can be applied to these graph patterns to limit the matching paths. Boolean logic operators, regular expressions and, string comparison operators can be used here.

=== Aggregations

There is an implicit group by when using aggregation functions such as `COUNT`.
++++
</div>
++++

++++
<div class="col-lg-9 ng-scope">
++++

=== Dissecting A Cypher Statement
Let's look a complex Cypher query (don't worry we'll build up our understanding of Cypher as we progress.

[width=100]
|===
| find      | `+MATCH (m:Movie)<-[:RATED]-(u:User)+`         | Search for an existing graph pattern
| filter    | `WHERE m.title CONTAINS "Matrix"`              | Filter matching paths to only those matching a predicate
| aggregate | `WITH m.title AS movie, COUNT(*) AS reviews`   | Count number of paths matched for each movie
| return    | `RETURN movie, reviews`                        | Specify columns to be returned by the statement
| order     | `ORDER BY reviews DESC`                        | Order by number of reviews, in descending order
| limit     | `LIMIT 5;`                                     | Only return first 4 records
|===


.Number of reviews for each Matrix movies
[source,cypher]
----
MATCH (m:Movie)<-[:RATED]-(u:User)
WHERE m.title CONTAINS "Matrix"
WITH m.title AS movie, COUNT(*) AS reviews
RETURN movie, reviews
ORDER BY reviews DESC
LIMIT 5;
----

Click on the block to put the query in the topmost window on the query editor. Hit the triangular icon:play-circle[] button or press kbd:[Ctrl+Enter] to run it and see the resulting visualization.

++++
</div>
++++

== Personalized Recommendations

Now let's start generating some recommendations. There are two basic approaches to recommendation algorithms.

++++
<div class="col-lg-6 ng-scope">
++++


=== Content Based Filtering

Recommend items that are similar to those that a user previously liked.

[subs=attributes]
++++
<img src="{img}/content1.png" class="img-responsive">
++++

."Products similar to the product you're looking at now"
[source,cypher,subs=attributes]
----
MATCH p=(m:Movie {title: "Net, The"})-[:ACTED_IN|:IN_GENRE|:DIRECTED*2]-()
RETURN p LIMIT 25
----

++++
</div>
++++

++++
<div class="col-lg-6 ng-scope">
++++

=== Collaborative Filtering

Use the preferences of other users in the network to find items to recommend.

[subs=attributes]
++++
<img src="{img}/cf1.png" class="img-responsive">
++++


."Users who bought this, also bought this other thing."
[source,cypher,subs=attributes]
----
MATCH (m:Movie {title: "Crimson Tide"})<-[:RATED]-(u:User)-[:RATED]->(rec:Movie)
RETURN rec.title AS recommendation, COUNT(*) AS usersWhoAlsoWatched
ORDER BY usersWhoAlsoWatched DESC LIMIT 25
----

++++
</div>
++++


== Content Based Filtering

The goal of content based filtering is to find similar items, using attributes (or traits) of the item. Using our movie data, one way we could define similarlity is movies that have common  genres.

[subs=attributes]
++++
<img src="{img}/genres.png" class="img-responsive">
++++


=== Similarity Based On Common Genres

.Find movies most similar to Inception based on shared genres
[source,cypher]
----
// Find similar movies by common genres
MATCH (m:Movie)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie)
WHERE m.title = "Inception"
WITH rec, COLLECT(g.name) AS genres, COUNT(*) AS commonGenres
RETURN rec.title, genres, commonGenres
ORDER BY commonGenres DESC LIMIT 10;
----

=== Personalized Recommendations Based on Genres

If we know what movies a user has watched we can use this information to recommend movies similar to those they've watched:

.Recommend movies similar to those the user has already watched
[source,cypher]
----
// Content recommendation by overlapping genres
MATCH (u:User {name: "Angelica Rodriguez"})-[r:RATED]->(m:Movie),
  (m)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie)
WHERE NOT EXISTS( (u)-[:RATED]->(rec) )
WITH rec, [g.name, COUNT(*)] AS scores
RETURN rec.title AS recommendation, rec.year AS year, 
COLLECT(scores) AS scoreComponents, 
REDUCE (s=0,x in COLLECT(scores) | s+x[1]) AS score 
ORDER BY score DESC LIMIT 10
----

=== Weighted Content Algorithm

Of course there are many more traits in addition to just genre that we can consider to compute similarity, such  
actors and directors. Let's use a weighted sum to score the recommendations based on the number of actors, genres, and directors they have in common to boost the score:

.Compute a weighted sum based on the number and types of overlapping traits
[source,cypher,subs=attributes]
----
// Find similar movies by common genres
MATCH (m:Movie) WHERE m.title = "Wizard of Oz, The"
MATCH (m)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie)

WITH m, rec, COUNT(*) AS gs

OPTIONAL MATCH (m)<-[:ACTED_IN]-(a:Actor)-[:ACTED_IN]->(rec)
WITH m, rec, gs, COUNT(a) AS as

OPTIONAL MATCH (m)<-[:DIRECTED]-(d:Director)-[:DIRECTED]->(rec)
WITH m, rec, gs, as, COUNT(d) AS ds

RETURN rec.title AS recommendation, (5*gs)+(3*as)+(4*ds) AS score ORDER BY score DESC LIMIT 100
----

== Content based similarity metrics

So far we've used the number of common traits as a way to score the relevance of our recommendations. Let's now consider a more robust way to quantify similarity, using a similarity metric. Similarity metrics are an important component used in generating personalized recommendations that allow us to quantify how similar two items (or as we'll see later, how similar two users preferences are).

++++
<div class="col-lg-3 ng-scope">
++++

=== Jaccard index

[subs=attributes]
++++
<img src="{img}/jaccard.png" class="img-responsive">
++++

The Jaccard index is a number between 0 and 1 that indicates how similar two sets are. The Jaccard index of two identical sets is 1. If two sets do not have a common element, then the Jaccard index is 0. The Jaccard is calculated by dividing the size of the intersection of two sets by the union of the two sets.

We can calculate the Jaccard index for sets of movie genres to determine how similar two movies are. 

++++
</div>
++++


++++
<div class="col-lg-9 ng-scope">
++++

.What movies are most similar to The Matrix based on Jaccard Similarity of genres?
[source,cypher]
----
MATCH (m:Movie {title: "Inception"})-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(other:Movie)
WITH m, other, COUNT(g) AS intersection, COLLECT(g.name) AS i
MATCH (m)-[:IN_GENRE]->(mg:Genre)
WITH m,other, intersection,i, COLLECT(mg.name) AS s1
MATCH (other)-[:IN_GENRE]->(og:Genre)
WITH m,other,intersection,i, s1, COLLECT(og.name) AS s2

WITH m,other,intersection,s1,s2

WITH m,other,intersection,s1+filter(x IN s2 WHERE NOT x IN s1) AS union, s1, s2

RETURN m.title, other.title, s1,s2,((1.0*intersection)/SIZE(union)) AS jaccard ORDER BY jaccard DESC LIMIT 100
----

We can apply this same apparoach to all "traits" of the movie (genre, actors, directors):

[source,cypher,subs=attributes]
----
MATCH (m:Movie {title: "Inception"})-[:IN_GENRE|:ACTED_IN|:DIRECTED]-(t)<-[:IN_GENRE|:ACTED_IN|:DIRECTED]-(other:Movie)
WITH m, other, COUNT(t) AS intersection, COLLECT(t.name) AS i
MATCH (m)-[:IN_GENRE|:ACTED_IN|:DIRECTED]-(mt)
WITH m,other, intersection,i, COLLECT(mt.name) AS s1
MATCH (other)-[:IN_GENRE|:ACTED_IN|:DIRECTED]-(ot)
WITH m,other,intersection,i, s1, COLLECT(ot.name) AS s2

WITH m,other,intersection,s1,s2

WITH m,other,intersection,s1+filter(x IN s2 WHERE NOT x IN s1) AS union, s1, s2

RETURN m.title, other.title, s1,s2,((1.0*intersection)/SIZE(union)) AS jaccard ORDER BY jaccard DESC LIMIT 100
----

++++
</div>
++++

== Collaborative Filtering - Movie ratings

++++
<div class="col-lg-6 ng-scope">
++++

[subs=attributes]
++++
<img src="{img}/cf2.png" class="img-responsive">
++++

TODO: graphic that shows ratings (arrows with data points)

Collaborative filtering is going to make use of rating data.

Steps:

1. Find simlar users in the network
1. Since similar users have similar preferences, what are the movies those similar users like?

++++
</div>
++++

++++
<div class="col-lg-6 ng-scope">
++++


=== Show all ratings by Misty Williams

[source,cypher]
----
// Show all ratings by Misty Williams
MATCH (u:User {name: "Misty Williams"})
MATCH (u)-[r:RATED]->(m:Movie)
RETURN *;
----

=== Find Misty's average rating

[source,cypher]
----
// Show all ratings by Misty Williams
MATCH (u:User {name: "Misty Williams"})
MATCH (u)-[r:RATED]->(m:Movie)
RETURN avg(r.rating) AS average;
----

=== What are the movies that Misty liked more than average?

[source,cypher]
----
// What are the movies that Misty liked more than average?
MATCH (u:User {name: "Misty Williams"})
MATCH (u)-[r:RATED]->(m:Movie)
WITH u, avg(r.rating) AS average
MATCH (u)-[r:RATED]->(m:Movie)
WHERE r.rating > average
RETURN *;
----

// TODO: we else liked movies that Misty rated highly


++++
</div>
++++

== Collaborative Filtering - The Wisdom of Crowds

Collaborative filtering

=== Simple Collaborative Filtering


[source,cypher,subs=attributes]
----
MATCH (u:User {name: "Cynthia Freeman"})-[:RATED]->(:Movie)<-[:RATED]-(o:User)
MATCH (o)-[:RATED]->(rec:Movie)
WHERE NOT EXISTS( (u)-[:RATED]->(rec) )
RETURN rec.title, rec.year, rec.plot
LIMIT 25
----

Of course this is just a simple appraoch, there are many problems with this query. Such as not normalizing based on popularity, or taking ratings into consideration. In the next section we will see how we can improve this approach using the **kNN method**. 

=== Only Consider Genres Liked By The User

Many recommender systems are a blend of collaborative filtering and content based approaches:

.For a particular user, what genres have a higher than average rating? Use this to score similar movies
[source,cypher,subs=attributes]
----
MATCH (u:User {name: "Andrew Freeman"})-[r:RATED]->(m:Movie)
WITH u, avg(r.rating) AS mean

MATCH (u)-[r:RATED]->(m:Movie)-[:IN_GENRE]->(g:Genre)
WHERE r.rating > mean

WITH u, g, COUNT(*) AS score

MATCH (g)<-[:IN_GENRE]-(rec:Movie)
WHERE NOT EXISTS((u)-[:RATED]->(rec))

RETURN rec.title AS recommendation, rec.year AS year, COLLECT(DISTINCT g.name) AS genres, SUM(score) AS sscore
ORDER BY sscore DESC LIMIT 10
----
 

== Collaborative Filtering - Similarity Metrics

++++
<div class="col-lg-3 ng-scope">
++++
=== Cosine Distance

[subs=attributes]
++++
<img src="{img}/cosine.png" class="img-responsive">
++++

Blurb about cosine distance. Something about number of dimensions? Scale?

++++
</div>
++++


++++
<div class="col-lg-9 ng-scope">
++++

[source,cypher,subs=attributes]
----
// Most similar users using Cosine similarity
MATCH (p1:User {name: "Cynthia Freeman"})-[x:RATED]->(m:Movie)<-[y:RATED]-(p2:User)
WITH COUNT(m) AS numbermovies, SUM(x.rating * y.rating) AS xyDotProduct,
SQRT(REDUCE(xDot = 0.0, a IN COLLECT(x.rating) | xDot + a^2)) AS xLength,
SQRT(REDUCE(yDot = 0.0, b IN COLLECT(y.rating) | yDot + b^2)) AS yLength,
p1, p2 WHERE numbermovies > 10
RETURN p1.name, p2.name, xyDotProduct / (xLength * yLength) AS sim 
ORDER BY sim DESC LIMIT 100;
----

++++
</div>
++++

== Collaborative Filtering - Similarity Metrics

=== Pearson similarity

++++
<div class="col-lg-3 ng-scope">
++++

[subs=attributes]
++++
<img src="{img}/pearson.png" class="img-responsive">
++++

Blurd about Pearson similarity. Benefit is that it can accomodate stochastic means across users. Scale (-1 to +1?) 

++++
</div>
++++

++++
<div class="col-lg-9 ng-scope">
++++


.Code caption goes here?
[source,cypher]
----
MATCH (u1:User {name:"Cynthia Freeman"})-[r:RATED]->(m:Movie)
WITH u1, avg(r.rating) AS u1_mean

MATCH (u1)-[r1:RATED]->(m:Movie)<-[r2:RATED]-(u2) 
WITH u1, u1_mean, u2, COLLECT({r1: r1, r2: r2}) AS ratings WHERE size(ratings) > 10

MATCH (u2)-[r:RATED]->(m:Movie)
WITH u1, u1_mean, u2, avg(r.rating) AS u2_mean, ratings

UNWIND ratings AS r

WITH sum( (r.r1.rating-u1_mean) * (r.r2.rating-u2_mean) ) AS nom,
     sqrt( sum( (r.r1.rating - u1_mean)^2) * sum( (r.r2.rating - u2_mean) ^2)) AS denom, 
     u1, u2 WHERE denom <> 0

RETURN u1.name, u2.name, nom/denom AS pearson 
ORDER BY pearson DESC LIMIT 100
----

++++
</div>
++++

== Collaborative Filtering - Neighborhood Based Recommendations


++++
<div class="col-lg-3 ng-scope">
++++

=== kNN

* Neigborhood based recommendation

++++
</div>
++++


++++
<div class="col-lg-9 ng-scope">
++++

.Code comment block goes here?
[source,cypher,subs=attributes]
----
MATCH (u1:User {name:"Cynthia Freeman"})-[r:RATED]->(m:Movie)
WITH u1, avg(r.rating) AS u1_mean

MATCH (u1)-[r1:RATED]->(m:Movie)<-[r2:RATED]-(u2) 
WITH u1, u1_mean, u2, COLLECT({r1: r1, r2: r2}) AS ratings WHERE size(ratings) > 10

MATCH (u2)-[r:RATED]->(m:Movie)
WITH u1, u1_mean, u2, avg(r.rating) AS u2_mean, ratings

UNWIND ratings AS r

WITH sum( (r.r1.rating-u1_mean) * (r.r2.rating-u2_mean) ) AS nom,
     sqrt( sum( (r.r1.rating - u1_mean)^2) * sum( (r.r2.rating - u2_mean) ^2)) AS denom, 
     u1, u2 WHERE denom <> 0

WITH u1, u2, nom/denom AS pearson 
ORDER BY pearson DESC LIMIT 10

MATCH (u2)-[r:RATED]->(m:Movie) WHERE NOT EXISTS( (u1)-[:RATED]->(m) )

RETURN m.title, SUM( pearson * r.rating) AS score
ORDER BY score DESC LIMIT 25
----

++++
</div>
++++

== Group Recommender System

++++
<div class="col-lg-6 ng-scope">
++++
Is it possible to make recommendations to a group of users? In the context of this example, can we recommend a restaurant that takes into account information about the individual users likes and dislikes? There are many strategies for aggregating a group of users preferences. This is an example of the application of "Social Choice Theory". For example:

* Plurality voting
* Average
* Multiplicative
* Borda Count
* Least misery
* Most Pleasure

++++
</div>
++++

++++
<div class="col-lg-6 ng-scope">
++++

.Some code caption
[source,cypher,subs=attributes]
----
// WIP
MATCH (u1:User {name: "Misty Williams"}),
      (u2:User {name: "Cynthia Freeman"})
      
WITH [u1,u2] AS users

UNWIND users AS u
MATCH (u)-[r:RATED]->(:Movie)-[:IN_GENRE]->(g:Genre)
WITH u, g, avg(r.rating) AS a

MATCH (rec:Movie)-[:IN_GENRE]->(g)
WHERE NOT EXISTS( (u)-[:RATED]->(rec))
WITH rec.title AS movie, collect(g.name) AS gs, collect(a) AS as, avg(a) AS score 
RETURN movie, gs, as, score+SIZE(gs) AS score
ORDER BY score DESC LIMIT 50
----

++++
</div>
++++
== Further Work

++++
<div class="col-lg-6 ng-scope">
++++

=== Resources

A BUNCH OF CALL TO ACTION STUFF HERE!!

++++
</div>
++++

++++
<div class="col-lg-6 ng-scope">
++++
=== Exercises

Extend these queries:

* **Temporal component** Preferences change over time, use the rating timestamp to consider how more recent ratings might be used to find more relevant recommendations.
* **Keyword extraction** Enhance the traits available using the plot description. How would you model extracted keywords for movies?
* **Image recognition using posters** There are several libraries and APIs that offer image recognition / tagging. Since we have movie poster images for each movie, how could we use these to enhance our recomendations?

++++
</div>
++++
